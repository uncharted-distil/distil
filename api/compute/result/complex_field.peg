package result

# Parser for an array field exported from a Python PANDAS dataframe via its to_csv() method.
# The exported file is RFC 4180 compatible, and is loaded using the CSV loader from the golang
# standard lib.  The loader handles most of the escaping.
#
# "[10, 20, 30, \"forty  & fifty\"]"
#
# parses to the Go type:
#
# []interface{"10", "20", "30", "forty & fifty"}
#

# generated go struct - accessible via `p` in attached code blocks
type ComplexField Peg {
    arrayElements
}

ComplexField <- array !.

# non-terminals 
array <- ws* obracket { p.pushArray() } array_contents cbracket { p.popArray() } /
    ws* oparen { p.pushArray() } array_contents comma? ws* cparen { p.popArray() }
array_contents <- ws* (item ws* (comma ws* item ws* )*)?
item <- array / string / <value> { p.addElement(buffer[begin:end]) }
string <- dquote_string / squote_string
dquote_string <- escdquote <(textdata / squote / lf / cr / obracket / cbracket / oparen / cparen / comma)*> escdquote { p.addElement(buffer[begin:end]) }
squote_string <- squote <(escsquote / escdquote / textdata / lf / cr / obracket / cbracket / oparen / cparen)*> squote { p.addElement(buffer[begin:end]) }
value <- negative? number+ (decimal_point number+)?

# terminals
ws <- ' '
comma <- ','
lf <- '\n'
cr <- '\r'
escdquote <- '\"'
escsquote <- '\\\''
squote <- '\''
obracket <- '['
cbracket <- ']'
oparen <- '('
cparen <- ')'
number <- [a-zA-Z0-9]
negative <- '-'
decimal_point <- '.'
textdata <- [a-zA-Z0-9 !#$&%*+\-./:;<->?\\^_`{|}~]
